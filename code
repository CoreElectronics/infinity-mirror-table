// This #include statement was automatically added by the Particle IDE.
#include <neopixel.h>

/*
 * v1.3
 *
 * This program drives the Core Electronics Infinity-Mirror project
 * Powered by Core Electronics
 * August 2017
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

#include "Particle.h"
#include "neopixel.h"
#include <math.h>

/*******************************************************************************
* Global Variables
*
******************************************************************************/
// States for the state-machine
enum statevar {
  state_off,
  state_rainbow,
  state_solid,
  state_comet,
};

// Set the States into an array to break the potentiometer into sectors without hardcoding them
// The order of States in the array == sector order.
const int modeArray[] = {state_off, state_rainbow, state_solid, state_comet};

static uint8_t state; // The state that the user demands
static uint8_t state_current; // The state currently being executed

const int strip_pin = 0; // The digital pin that drives the LED strip
const int num_leds = 160; // Number of LEDs in the strip. Shouldn't need changing unless you hack the hardware
const int ADC_precision = 4095; // Particle use 12bit ADCs. If you wish to port to a different platform you might need to redefine the ADC precision eg: 1023 for Arduino UNO

SYSTEM_MODE(SEMI_AUTOMATIC); // Release state

/*
 * Hardware Definitions
 * potentiometers are used as easily reconfigurable controls
 */

int pot_1 = 2; // left    - selects the mode
int pot_2 = 1; // middle  - use varies between demos. Can control animation speed or colour
int pot_3 = 0; // right   - use varies between demos. Can control brightness of white
Adafruit_NeoPixel strip(num_leds, strip_pin, WS2812);


void setup()
{
    strip.begin();
    strip.show(); // Initialize all pixels to off
}

/* **************** LOOP ****************  */
void loop()
{
    
    if (System.buttonPushed() > 1) {
        if( !Particle.connected() ){
            Particle.connect();
        }
    }
    
    
    // Read potentiometer values for user-input
    int state = getState(pot_1);    // Select the operation mode
    int opt1 = analogRead(pot_2);   // Select animation speed
    int opt2 = analogRead(pot_3);   // A general-purpose option for other effects
    
    
    // State Machine
    switch(state){
        case state_off:
            clearStrip();   // "Off" state.
            break;
        
        case state_rainbow:
            rainbow(pot_2); // Adafruit's rainbow demo, modified for seamless wraparound. We are passing the Pot # instead of the option because delay value needs to be updated WITHIN the rainbow function. Not just at the start of each main loop.
            break;
        
        case state_solid:
            solid(opt1, opt2); // Show user-set solid colour.
            break;
            
        case state_comet:
            demo(); // An under-construction comet demo.
            break;
            
        default:
            break;
        
    }
}

/* **************** FUNCTIONS ****************  */
// Break potentiometer rotation into flexible sectors based on the constant modeArray
int getState(int pot)
{
    float val = analogRead(pot);
    float aValNum(sizeof(modeArray)/sizeof(*modeArray));
    float potSegLength(ADC_precision/aValNum);
    
    float prevSegMax;
    float currSegMax;
    int i;
    
    for (i = 0; i < aValNum; i++ ) 
    {
        currSegMax = (i + 1) * potSegLength;    // Set the current segment maximum
        if (i > 0) {
            prevSegMax = (i * potSegLength);    // Set the previous segment maximum
        }
        else {
            prevSegMax = 0;                     // first segment minimum
        }

        if (val > prevSegMax && val <= currSegMax) {
            return modeArray[i];
        }
    }
}

// Convert an ADC reading into a 0-100ms delay
int getDelay(int pot){
    float potVal = analogRead(pot);
    return map(potVal,0,ADC_precision,100,0);
}


/* Run the comet demo
 * This feature is largely experimental and quite incomplete.
 * The idea is to involve multiple comets that can interact by colour-addition
 */
void demo(void)
{
    uint16_t i, j, k;
    uint16_t ofs = 15;
    
    for (j=0; j<strip.numPixels(); j++){
        clearStrip();
        
        comet(j,1);
        
        strip.show();
        delay(5);
        if(getState(pot_1) != state_comet) break; // Check if mode knob is still on this mode
    }
}

void comet2(void)
{
    uint16_t i, j, k;
    uint16_t ofs = 15;
    
    for (j=0; j<strip.numPixels(); j++){
        clearStrip();
        
        comet(j,1);
        
        strip.show();
        delay(5);
        if(getState(pot_1) != state_comet) break; // Check if mode knob is still on this mode
    }
}

/*
 * Draw a comet on the strip and handle wrapping gracefully.
 * Arguments:
 *      - pos: the pixel index of the comet's head
 *      - dir: the direction that the tail should point
 *
 * TODO: 
 *      - Handle direction gracefully. In the works but broken.
 *      - Handle multiple comets
 */ 
void comet(uint16_t pos, bool dir) {
    float headBrightness = 255;                 // Brightness of the first LED in the comet
    uint8_t bright = uint8_t(headBrightness);   // Initialise the brightness variable
    uint16_t len = 20;                          // Length of comet tail
    double lambda = 0.3;                        // Parameter that effects how quickly the comet tail dims
    double dim = lambda;                        // initialise exponential decay function
    
    strip.setPixelColor(pos, strip.Color(0,bright,0)); // Head of the comet
    
    
    if(dir) {
        for(uint16_t i=1; i<len; i++){
            // Figure out if the current pixel is wrapped across the strip ends or not, light that pixel
            if( pos - i < 0 ){ // Wrapped
                strip.setPixelColor(strip.numPixels()+pos-i, strip.Color(0,bright,0));
            } else { // Not wrapped
                strip.setPixelColor(pos-i, strip.Color(0,bright,0));
            }
            bright = uint8_t(headBrightness * exp(-dim)); // Exponential decay function to dim tail LEDs
            dim += lambda;
        }
        
    } else { // Comet is going backwards *** BROKEN: TODO fix ***
        for(uint16_t i=1; i<len; i++){
            // Figure out if the current pixel is wrapped across the strip ends or not, light that pixel
            if( pos + i > strip.numPixels() ){ // Wrapped
                strip.setPixelColor(strip.numPixels()-pos-i, strip.Color(0,bright,0));
            } else { // Not wrapped
                strip.setPixelColor(pos+i, strip.Color(0,bright,0));
            }
            // Dim the tail of the worm. This probably isn't the best way to do it, but it'll do for now. 
            // TODO: dim while respecting the length of the worm. For long worms this will dim to zero before the end of worm is reached.
            bright *= 0.75;
        }
    }
}


void clearStrip(void){
    uint16_t i;
    for(i=0; i<strip.numPixels(); i++){
            strip.setPixelColor(i, strip.Color(0,0,0));
        }
        strip.show();
        delay(1);
}


void rainbow(int pot) {
//   uint16_t j;
  float i, baseCol;
  float colStep = 256.0 / strip.numPixels();

  for(baseCol=0; baseCol<256; baseCol++) { // Loop through all colours
    for(i=0; i<strip.numPixels(); i++) {   // Loop through all pixels
        strip.setPixelColor( i, Wheel(int(i*(colStep)+baseCol) & 255) ); // This line seamlessly wraps the colour around the table.
    }
    strip.show();
    delay(getDelay(pot));
    
    if(getState(pot_1) != state_rainbow) break; // Check if mode knob is still on this mode
  }
}


// Input a value 0 to 255 to get a color value.
// The colours are a transition r - g - b - back to r.
uint32_t Wheel(byte WheelPos) {
  if(WheelPos < 85) {
   return strip.Color(WheelPos * 3, 255 - WheelPos * 3, 0);
  } else if(WheelPos < 170) {
   WheelPos -= 85;
   return strip.Color(255 - WheelPos * 3, 0, WheelPos * 3);
  } else {
   WheelPos -= 170;
   return strip.Color(0, WheelPos * 3, 255 - WheelPos * 3);
  }
}


// Display a single solid colour from the Wheel(), or show white with variable brightness
int solid(int colour, int bright){
    bright = map(bright,0,ADC_precision,5,255);
    int col = map(colour,0,ADC_precision,0,255);
    uint16_t i;
    
    if (col > 245) {
        // Set to white
        for(i=0; i<strip.numPixels(); i++){
            strip.setPixelColor(i, strip.Color(bright,bright,bright));
        }
        
    } else {
        // User-defined colour
        for(i=0; i<strip.numPixels(); i++){
                strip.setPixelColor(i, Wheel(col));
        }
    }    
    strip.show();
    delay(50);
}
